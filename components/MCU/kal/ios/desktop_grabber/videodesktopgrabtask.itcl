########################################################################
#
#  ISABEL: A group collaboration tool for the Internet
#  Copyright (C) 2009 Agora System S.A.
#  
#  This file is part of Isabel.
#  
#  Isabel is free software: you can redistribute it and/or modify
#  it under the terms of the Affero GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#  
#  Isabel is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  Affero GNU General Public License for more details.
# 
#  You should have received a copy of the Affero GNU General Public License
#  along with Isabel.  If not, see <http://www.gnu.org/licenses/>.
#
########################################################################

#########################################################################
#
# videodesktopgrabtask.itcl
#
# Description
#
#   Task to start and access to the C video desktop grabber daemon.
#
#########################################################################

class VideoDesktopGrabberTaskC {

    #------------------
    # Public INTERFACE
    #------------------

    constructor {} {}
    destructor {}

    public method RDO {cmd}
    public method RPC {cmd}
    public method RCB {cmd cb}


    # Objects that are warned when the daemon dies and
    # it is restarted again.
    # When the daemon is running again, this task executes 
    # all the callbacks stored in the $listeners list.
    public method AddListener    {cb}
    public method RemoveListener {cb}
    
    #-------------------
    # PRIVATE INTERFACE
    #-------------------

    # 1 if the created object is working.
    private variable active 0
    
    # video daemon object
    private variable vdobj ""

    # Listeners.
    # This is a list of methods to call when the video daemon restart.
    private variable listeners ""

    #----

    # Start the daemon.
    private method CreateDaemon {}

    # Executed when the daemon exits.
    private method NoCCDaemon {}

    # Executes all the callbacks stored in the $listeners list.
    private method ExecListeners {}

}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::constructor {} {
    global env
    
    WriteTraceMsg "Video Desktop Grabber Task: constructing ..."

    CreateDaemon
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::destructor {} {
    
    set active 0

    if {$vdobj != ""} {
	delete object $vdobj
	set vdobj ""
    }
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::CreateDaemon {} {

    if {$vdobj != ""} return

    WriteTraceMsg "Video Desktop Grabber Task: Creating a new video daemon ..."

    set no_cc_cb [code $this NoCCDaemon]
    set vdobj [VideoDesktopGrabberDaemonC "#auto" $no_cc_cb]
    WriteTraceMsg "Video Desktop Grabber Task: The new video daemon object was created. Connecting ..."
    if {[catch {$vdobj Start} res]} {
	WriteTraceMsg "Video Desktop Grabber Task: Unsuccessful connection to the new video daemon."
	delete object $vdobj
	NoCCDaemon
	ErrorTraceMsg "Video Desktop Grabber Task: video daemon can not be created: $res"
	return
    }

    WriteTraceMsg "Video Desktop Grabber Task: Successful connection to the new video daemon."
    WriteTraceMsg "Video Desktop Grabber Task: Activating ..."
    
    set active 1

    WriteTraceMsg "Video Desktop Grabber Task: Activation done."

    #-------------------
    
    WriteTraceMsg "Video Desktop Grabber Task: Begining calling listeners."
    ExecListeners
    WriteTraceMsg "Video Desktop Grabber Task: Done calling Listeners."
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::NoCCDaemon {} {
    global env

    set active 0
    set vdobj  ""

    if {[info exists env(ISABEL_AUTORESTART)]} {
	
	if {$env(ISABEL_AUTORESTART) != 0} {
	    set restart 1
	} else {
	    set restart 0
	}

    } else {
	set restart 1
    }

    if {$restart} {
	after 1000 after idle [code $this CreateDaemon]
    } else {
	WarningTraceMsg "Video Desktop Grabber Task: The video daemon is dead and it will no be restarted."
    }

}

#--------------------------------------------------------------

body VideoDesktopGrabberTaskC::RDO {cmd} {

    if {! $active} return
    
    $vdobj RDO $cmd
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::RPC {cmd} {

    if {! $active} return ""
    
    $vdobj RPC $cmd
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::RCB {cmd cb} {

    if {! $active} return
    
    $vdobj RCB $cmd $cb
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::AddListener {cb} {
    
    lappend listeners $cb
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::RemoveListener {cb} {

    set listeners [lremove $listeners $cb]
}

#---------------------------------------------------------------

body VideoDesktopGrabberTaskC::ExecListeners {} {

    foreach cb $listeners {
    WriteTraceMsg "Video Desktop Grabber Task: Listener invocation: $cb."
	if {[catch {uplevel "#0" $cb} emsg]} {
	    ErrorTraceMsg "- Video Desktop Grabber Task:: Callback error: \"$cb\" produces \"$emsg\"" 
	}
    }
}

#---------------------------------------------------------------

