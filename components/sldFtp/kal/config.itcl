########################################################################
#
#  ISABEL: A group collaboration tool for the Internet
#  Copyright (C) 2009 Agora System S.A.
#  
#  This file is part of Isabel.
#  
#  Isabel is free software: you can redistribute it and/or modify
#  it under the terms of the Affero GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#  
#  Isabel is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  Affero GNU General Public License for more details.
# 
#  You should have received a copy of the Affero GNU General Public License
#  along with Isabel.  If not, see <http://www.gnu.org/licenses/>.
#
########################################################################
#########################################################################
#
# config.itcl
#
# SldFtp configurator.
#
#########################################################################

class SldFtpConfigC {

    #------------------
    # PUBLIC INTERFACE
    #------------------

    # Change user interface visibility.
    public proc Show {}
    public proc Hide {}
    public proc ToggleShowHide {}

    # Set local bandwidth.
    public proc SetBWLocal {bw}

    #-------------------
    # PRIVATE INTERFACE
    #-------------------

    # toplevel widget.
    private common wroot .sldftp_config_tl

    # Current bandwidth.
    private common currentbw

    # Create the user interface.
    private proc GUI {}

    # Set global bandwidth.
    private proc SetBWGlobal {bw}
}

#---------------------------------------------------------------------------

body SldFtpConfigC::GUI {} {

    if {[winfo exist $wroot]} { 
	return
    }

    set currentbw [session Activity GetResource SldFtp Ftp.bw 128]

    toplevel $wroot -screen [session Environ cget -ctrl_display]

    wm title $wroot "SldFtp Config"

    label $wroot.l -text "SldFtp Configuration"
    pack  $wroot.l -side top

    #--------------------------------

    button $wroot.close -text Dismiss -command SldFtpConfigC::Hide

    pack $wroot.close -side bottom -pady 8

    #--------------------------------

    frame $wroot.g -bd 3 -relief groove 
    pack  $wroot.g -side top -expand 1 -fill both -padx 5 -pady 5 -ipadx 5 -ipady 5

    button $wroot.kk
    set font [$wroot.kk cget -font]	
    destroy $wroot.kk
    
    label $wroot.g.lbw -text "BW (kbps):" -font $font
    
    scale $wroot.g.sbw \
	    -from 5 -to 3500 \
	    -highlightthickness 0 \
	    -resolution 5 \
	    -width 10 \
	    -orient horizontal \
	    -showvalue 1 \
	    -variable [scope currentbw] 
    InitScaleValue $wroot.g.sbw $currentbw [code SetBWGlobal]
    
    #--------------------------------

    grid $wroot.g.lbw     -row 0 -column 0 -sticky es  
    grid $wroot.g.sbw     -row 0 -column 1 -sticky we
    
    grid columnconfigure $wroot.g 1 -weight 1
    
    #--------------------------------
    
}

#---------------------------------------------------------------------------

body SldFtpConfigC::Show {} {
    
    if {![winfo exist $wroot]} { 
	GUI
    } else {
	wm deiconify $wroot
    }
}

#---------------------------------------------------------------------------

body SldFtpConfigC::Hide {} {

    if {[winfo exist $wroot]} { 
	wm withdraw $wroot
    }
}

#---------------------------------------------------------------------------

body SldFtpConfigC::ToggleShowHide {} {
    
    if {![winfo exist $wroot]} { 
	Show
    } else {
	if {[wm state $wroot] != "normal"} {
	    Show
	} else {
	    Hide
	}
    }
}

#---------------------------------------------------------------------------

body SldFtpConfigC::SetBWGlobal {bw} {

    set currentbw $bw
    network MasterRequest EI Activity SetResource SldFtp Ftp.bw $bw
    network SiteBroadcast COMP SldFtp SldFtpConfigC::SetBWLocal $bw
}

#---------------------------------------------------------------------------

body SldFtpConfigC::SetBWLocal {bw} {

    set currentbw $bw
    session Activity SetResource SldFtp Ftp.bw $bw
}

#---------------------------------------------------------------------------

